#lang rzk-1

-- Prerequisites (HoTT definitions)

#def prod : (A : U) -> (B : U) -> U
  := \A -> \B -> ∑ (x : A), B

#def isweq : (A : U) -> (B : U) -> (f : (_ : A) -> B) -> U
  := \A -> \B -> \f -> ∑ (g : (_ : B) -> A), prod ((x : A) -> g (f x) =_{A} x) ((y : B) -> f (g y) =_{B} y)

#def weq : (A : U) -> (B : U) -> U
  := \A -> \B -> ∑ (f : (_ : A) -> B), isweq A B f

-- Section 3

-- Simplices

#def Δ¹ : (t : 2) -> TOPE
  := \(t : 2) -> TOP

#def Δ² : (t : 2 * 2) -> TOPE
  := \(t, s) -> s <= t

#def Δ³ : (t : 2 * 2 * 2) -> TOPE
  := \((t1, t2), t3) -> t3 <= t2 /\ t2 <= t1

-- Boundaries

#def ∂Δ¹ : (t : 2) -> TOPE
  := \(t : 2) -> 0_2 === t \/ 1_2 === t

#def ∂Δ² : (t : 2 * 2) -> TOPE
  := \(t, s) -> (0_2 === s /\ s <= t) \/ (s === t) \/ (s <= t /\ t === 1_2)

-- Connection squares

#def V : (A : U) ->
         (f : <{t : 2 | TOP} -> A[BOT |-> recBOT]>) ->
         <{ts : 2 * 2 | TOP} -> A[BOT |-> recBOT]>
  := \A -> \f ->
     \(t, s) -> recOR(t <= s, s <= t, f s, f t)

#def Λ : (A : U) ->
         (f : <{t : 2 | TOP} -> A[BOT |-> recBOT]>) ->
         <{ts : 2 * 2 | TOP} -> A[BOT |-> recBOT]>
  := \A -> \f ->
     \(t, s) -> recOR(t <= s, s <= t, f t, f s)

-- Products of shapes

#def shapeProd : (I : CUBE) -> (J : CUBE) ->
                 (psi : (t : I) -> TOPE) ->
                 (chi : (s : J) -> TOPE) ->
                 (ts : I * J) -> TOPE
  := \I -> \J -> \psi -> \chi ->
     \(t, s) -> psi t /\ chi s

#def Δ¹×Δ¹ : (t : 2 * 2) -> TOPE
  := shapeProd 2 2 Δ¹ Δ¹

#def Δ²×Δ¹ : (t : 2 * 2 * 2) -> TOPE
  := shapeProd (2 * 2) 2 Δ² Δ¹

-- Section 4

-- [RS17, Theorem 4.1]
#def flip-ext-fun
  : (I : CUBE) ->
    (psi : (t : I) -> TOPE) ->
    (phi : {(t : I) | psi t} -> TOPE) ->
    (X : U) ->
    (Y : <{t : I | psi t} -> (x : X) -> U >) ->
    (f : <{t : I | phi t} -> (x : X) -> Y t x >) ->
    weq <{t : I | psi t} -> (x : X) -> Y t x [phi t |-> f t]>
        ((x : X) -> <{t : I | psi t} -> Y t x [phi t |-> f t x]>)
  := \I -> \psi -> \phi -> \X -> \Y -> \f ->
     (\k -> \x -> \{t : I | psi t} -> k t x,
      (\k -> \{t : I | psi t} -> \x -> (k x) t,
       (\k -> refl_{k},
        \k -> refl_{k})))

-- [RS17, Theorem 4.2 (part 1)]
#def uncurry-ext-weq
  : (I : CUBE) ->
    (J : CUBE) ->
    (psi  : (t : I) -> TOPE) ->
    (phi  : {(t : I) | psi t} -> TOPE) ->
    (zeta : (s : J) -> TOPE) ->
    (chi  : {(s : J) | zeta s} -> TOPE) ->
    (X : <{t : I | psi t} -> <{s : J | zeta s} -> U > >) ->
    (f : <{(t, s) : I * J | (phi t /\ zeta s) \/ (psi t /\ chi s)} -> X t s >) ->
    weq <{t : I | psi t} ->
          <{s : J | zeta s} -> X t s [ chi s |-> f (t, s) ]>
            [ phi t |-> \{s : J | zeta s} -> f (t, s) ]>
        <{(t, s) : I * J | psi t /\ zeta s} -> X t s
          [ (phi t /\ zeta s) \/ (psi t /\ chi s) |-> f (t, s) ]>
  := \I -> \J -> \psi -> \phi -> \zeta -> \chi -> \X -> \f ->
     (\k -> \(t, s) -> k t s,
      (\k -> \{t : I | psi t} -> \{s : J | zeta s} -> k (t, s),
       (\k -> refl_{k}, \k -> refl_{k})))

-- #def uncurry_ext : (I : CUBE) -> (J : CUBE) -> (psi : (t : I) -> TOPE) -> (zeta : (s : J) -> TOPE) -> (X : <{t : I | psi t} -> <{s : J | zeta s} -> U> >) -> (chi : {(t : I) | psi t} -> TOPE) -> (phi : {(s : J) | zeta s} -> TOPE) -> (f : <{(t, s) : I * J | psi t /\ zeta s} -> X t s >) -> (_ : <{t : I | psi t} -> <{s : J | zeta s} -> X t s [chi s |-> f (t, s)]> [phi t |-> \s -> f (t, s)]>) -> <{(t, s) : I * J | psi t /\ zeta s} -> X t s [(phi t /\ zeta s) \/ (psi t /\ chi s) |-> f (t, s)]>
--   := \I -> \J -> \psi -> \zeta -> \X -> \chi -> \phi -> \f -> \k -> \(t, s) -> k t s
